actor Malumax 1010
{
	//$Category "Monsters"
	Health 3500
	Radius 16
	Height 64
	Scale 1
	Mass 300
	Speed 20
	PainChance 64
	Monster
	+DROPOFF
	+BOSS
	+DONTMORPH
	+FULLVOLACTIVE
	+NORADIUSDMG
	+NOTARGET
	+NOICEDEATH
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "wizard/sight"
	AttackSound "wizard/attack"
	PainSound "wizard/pain"
	ActiveSound "wizard/active"
	DeathSound "wizard/death"

	Obituary "%o had an encounter with Malumax."

	var int user_cataclysm_state;
	var int user_x;

	States
	{
	Spawn:
		MALM A 0 NoDelay { user_cataclysm_state = 0; }
		MALM AB 12 A_Look
		loop
	See:
		MALM AABBAACC 4 A_Chase
		MALM A 0 A_Jump(8, "Teleport")
		loop
	Pain:
		MALM D 0 A_ChangeFlag("FRIGHTENED", 1)
		MALM D 0 A_Jump(64, "Teleport")
		MALM D 3
		MALM D 3 A_Pain
		goto See
	Missile:
		MALM E 0 {
			if (user_cataclysm_state == 0 && ((1.0 * health / GetSpawnHealth()) < .8))
			{
				ACS_Execute(996, 0, 1);
			}
		}
		MALM E 0 A_Jump(32, "Teleport")
		MALM E 0 A_Jump(256, "Hydra", "HomingAttack", "FlameNova")
		goto See
	Teleport:
		MALM AAAFFFAAAGGGAAAFFFAAAGGG 2 {
			A_FaceTarget;
			for (user_x = random(-5, 22); user_x < 360; user_x+=random(22, 65))
			{
				A_SpawnProjectile("PhoenixFX2", 5, 0, user_x, CMF_ABSOLUTEPITCH);
			}
		}
		MALM E 0 {
			A_SpawnItem("TeleportFog", 0, 32);
			A_ChangeFlag("INVISIBLE", 1);
			A_ChangeFlag("SHOOTABLE", 0);
		}
		MALM E 70
		MALM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 0 A_Chase("", "", CHF_NOPLAYACTIVE)
		MALM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 0 A_Chase("", "", CHF_NOPLAYACTIVE)
		MALM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 0 A_Chase("", "", CHF_NOPLAYACTIVE)
		MALM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 0 A_Chase("", "", CHF_NOPLAYACTIVE)
		MALM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 0 A_Chase("", "", CHF_NOPLAYACTIVE)
		MALM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 0 A_Chase("", "", CHF_NOPLAYACTIVE)
		MALM E 0 {
			A_SpawnItem("TeleportFog", 0, 32);
			A_ChangeFlag("INVISIBLE", 0);
			A_ChangeFlag("SHOOTABLE", 1);
		}
		MALM AAAFFFAAAGGGAAAFFFAAAGGG 2 {
			A_RadiusThrust(50, 128, RTF_NOTMISSILE | RTF_THRUSTZ, 1);
			A_FaceTarget;
			for (user_x = random(-5, 22); user_x < 360; user_x+=random(22, 65))
			{
				A_SpawnProjectile("PhoenixFX2", 5, 0, user_x, CMF_ABSOLUTEPITCH);
			}
		}
		MALM A 0 A_ChangeFlag("FRIGHTENED", 0)
		goto See		
	Hydra:
		MALM E 0 A_CheckRange(512, "HomingAttack")
		MALM E 4 A_FaceTarget
		MALM E 12 A_SpawnProjectile("BawssHydra", 32, 0, random(0, 360), CMF_ABSOLUTEPITCH, 60)
		goto See
	HomingAttack:
		MALM E 4 A_FaceTarget
		MALM E 12 A_SpawnProjectile("BawssHoming")
		goto See
	FlameNova:
		MALM E 4 A_FaceTarget
		MALM AAAFFFAAAGGGAAAFFFAAAGGG 2 {
			A_FaceTarget;
			A_SpawnParticle("ff ff 00", SPF_FULLBRIGHT | SPF_RELATIVE, 60, 5, random(0,360), 0, 0, 0, frandom(0, 1.5), 0, frandom(0, 8.0), 0, 0, -0.5, frandom(0.5, 1.0), -1);
		}
		MALM E 12 {
 			for (user_x = 0; user_x < 360; user_x+=3)
			{
				A_SpawnProjectile("BawssFlameSpike", 32, 0, user_x);
			}
		}
		goto See
	Death:
		FDTH C 6 { A_Scream; A_NoBlocking; }
		FDTH DEFGHIJKLMNOPQR 6
		TNT1 A 1 ACS_Execute(996, 0, 0)
		Stop
	}
}

actor BawssFlameSpike
{
	Radius 5
	Height 12
	Speed 14
	FastSpeed 20
	Damage 2
	Projectile
	+FLOORHUGGER
	ExplosionDamage 16
	DeathSound "minotaur/fx2hit"
	States
	{
	Spawn:
		FX13 BCDE 5 Bright
		Loop
	Death:
		FX13 I 4 Bright A_Explode
		FX13 JKLM 4 Bright
		Stop
	}
}

actor BawssHoming
{
	Radius 10
	Height 6
	Speed 10
	FastSpeed 15
	Damage 9
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	+SEEKERMISSILE
	RenderStyle Normal
	DamageType Fire
	SeeSound "weapons/phoenixshoot"
	DeathSound "weapons/phoenixhit"

	States
	{
	Spawn:
		SBS1 ABCD 3 Bright A_SeekerMissile(5, 10, SMF_PRECISE)
		Loop
	Death:
		FX06 DEFG 5 Bright
		Stop
	}
}

actor BawssHydra
{
	Radius 10
	Height 6
	Speed 10
	FastSpeed 15
	Damage 9
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	RenderStyle Normal

	States
	{
	Spawn:
		SBS1 ABCD 3 Bright
		Loop
	Death:
		FX06 DEFG 5 Bright
		FX06 G 0 A_SpawnItem("Hydra1")
		Stop
	}
}

actor Hydra1
{
	Health 10
	Speed 0
	Monster
	+NODAMAGE
	-SOLID
	-SHOOTABLE
	-COUNTKILL
	RenderStyle Add
	Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.50,0.25]"
	+ALWAYSFAST

	States
	{
	Spawn:
		SSDV JJJJJJJJJJ 1 A_Look
		SSDV J 1 A_Die
		Goto Death
	See:
		SSDV JIHEA 5
	See2:
		SSDV AAAAAAAAAAAAAAAAA 5 A_Chase
		SSDV A 1 A_Die
		Goto Death
	Missile:
		SSDV ABC 10 A_FaceTarget
		SSDV DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD 2 A_SpawnProjectile("PhoenixFX2", 24)
		SSDV D 0 A_Die
		goto Death
	Death:
		SSDV AEHIJ 5
		Stop
	}
}
