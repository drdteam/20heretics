// New stat screen by Gutawer which draws the author name under the map name automatically.
// Code style is inconsistent because a lot of it is copied from the normal status bar classes, and I'm really
// not in the mood to make it consistent :P

// Additions by Marisa to show a text scroll on the "entering level" section
// I have my own code style too, sorry (at least this way you can tell who made the additions)

class TwentyHCPStatusScreenBase : StatusScreen {

	String exittext;
	double textpos;
	bool textdone[MAXPLAYERS];
	bool present[MAXPLAYERS];

	void ConstructLines( int skip = 0 )
	{
		Array<String> data;
		int lump;
		if ( !(lump = Wads.FindLump("EXITTEXT")) )
			ThrowAbortException("Missing EXITTEXT lump!");
		Wads.ReadLump(lump).Split(data,"\n");
		int start = -1;
		int end = -1;
		for ( int i=0; i<data.size(); i++ )
		{
			if ( (data[i].Left(2) == "//") && (skip-- <= 0) )
			{
				start = i+2;
				break;
			}
		}
		for ( int i=start; i<data.size(); i++ )
		{
			if ( data[i].Left(2) == "//" )
			{
				end = i-1;
				break;
			}
		}
		// very ugly, hastily cobbled together code for turning paragraphs into single lines
		for ( int i=start; i<end; i++ )
		{
			if ( data[i].length() == 0 ) exittext = String.Format("%s\n\n",exittext);
			else exittext = String.Format("%s %s",exittext,data[i]);
		}
	}

	override void initShowNextLoc()
	{
		Super.initShowNextLoc();
		// load text scroll
		exittext = "";
		textpos = 0.0;
		for ( int i=0; i<MAXPLAYERS; i++ )
		{
			textdone[i] = false;
			present[i] = false;
		}
		if ( wbs.next ~== "MAP02" ) ConstructLines(0);
		else if ( wbs.next ~== "MAP03" ) ConstructLines(1);
		else if ( wbs.next ~== "MAP04" ) ConstructLines(2);
		else if ( wbs.next ~== "MAP05" ) ConstructLines(3);
		else if ( wbs.next ~== "MAP06" ) ConstructLines(4);
		else if ( wbs.next ~== "MAP07" ) ConstructLines(5);
		else if ( wbs.next ~== "MAP08" ) ConstructLines(6);
		else if ( wbs.next ~== "MAP09" ) ConstructLines(7);
		else if ( wbs.next ~== "MAP10" ) ConstructLines(8);
		else if ( wbs.next ~== "MAP11" ) ConstructLines(9);
		else if ( wbs.next ~== "MAP12" ) ConstructLines(10);
		else if ( wbs.next ~== "MAP13" ) ConstructLines(11);
		else if ( wbs.next ~== "MAP14" ) ConstructLines(12);
		else if ( wbs.next ~== "SECRET" ) ConstructLines(14);
		else if ( (wbs.current ~== "SECRET") && (wbs.next ~== "MAP15") ) ConstructLines(15);
		else if ( wbs.next ~== "MAP15" ) ConstructLines(13);
		else if ( wbs.next ~== "FINAL" ) ConstructLines(16);
		else if ( wbs.next ~== "CREDITS" ) ConstructLines(17);
		present[wbs.pnum] = true;
	}

	override void updateShowNextLoc()
	{
		bool alldone = true;
		for ( int i=0; i<MAXPLAYERS; i++ ) if ( present[i] && !textdone[i] ) alldone = false;
		if ( alldone || acceleratestage ) initNoState();
		else snl_pointeron = (cnt & 31) < 20;
		textpos += CleanYfac*0.2;
	}

	override void drawShowNextLoc()
	{
		bg.drawBackground(curState, true, snl_pointeron);
		// draws which level you are entering..
		int cliptop, clipbottom, clipleft, clipright;
		cliptop = hcpDrawEL()+8*CleanYfac;
		clipbottom = screen.GetHeight()-8*CleanYfac;
		clipleft = 8*CleanXfac;
		clipright = screen.GetWidth()-8*CleanXfac;
		double clipmid = (cliptop+clipbottom)/2;
		double clipheight = clipbottom-cliptop;
		double basey = clipbottom-textpos;
		BrokenLines lines = SmallFont.BreakLines(exittext,Screen.GetWidth()/CleanXfac - clipleft*2);
		// draw text scroll
		int count = lines.Count();
		double height = SmallFont.GetHeight()*1.2*CleanYfac;
		int longest = 0;
		double alph;
		for ( int i=0; i<count; i++ ) if ( lines.StringWidth(i) > longest ) longest = lines.StringWidth(i);
		for ( int i=0; i<count; i++ )
		{
			alph = clamp(cos((abs(basey-clipmid)/clipheight)*200.0),0.0,1.0);
			Screen.DrawText(SmallFont,Font.CR_White,(screen.GetWidth()-longest*CleanXfac)/2,int(basey),lines.StringAt(i),DTA_CleanNoMove,true,DTA_ClipTop,cliptop,DTA_ClipBottom,clipbottom,DTA_ClipLeft,clipleft,DTA_ClipRight,clipright,DTA_Alpha,alph);
			basey += height;
			if ( (i == count-1) && (basey <= 0) ) textdone[wbs.pnum] = true;
		}
	}

	int drawNameStringOnly(int y, string levelname) {
		if (levelname.Length() > 0) {
			int h = 0;
			int lumph = mapname.mFont.getHeight() * cleanYfac;

			BrokenLines lines = mapname.mFont.breakLines(levelname, screen.GetWidth() / CleanXfac);

			int count = lines.Count();
			for (int i = 0; i < count; i++) {
				screen.drawText(mapname.mFont, mapname.mColor, (screen.GetWidth() - lines.StringWidth(i) * CleanXfac) / 2, y + h, lines.StringAt(i), DTA_CleanNoMove, true);
				h += lumph;
			}
			return y + h + lumph/4;
		}
		return 0;
	}

	int hcpDrawLF() {
		int y = TITLEY * CleanYfac;

		string levelName = lnametexts[0];
		Array<string> splitStrings;
		levelName.split(splitStrings, " - by: ");
		if (splitStrings.size() == 2) {
			y = drawNameStringOnly(y, splitStrings[0]);
			y -= 24;

			GIFont authorFont;
			authorFont.fontName = "smallfont";
			authorFont.color = "gold";
			patchInfo authorPatchInfo;
			authorPatchInfo.init(authorFont);
			y = drawPatchText(y, authorPatchInfo, splitStrings[1]);
			y += 4;

			// Adjustment for different font sizes for map name and 'finished'.
			y -= ((mapname.mFont.getHeight() - smallfont.getHeight() - finished.mFont.getHeight()) * cleanYfac) / 4;

			// draw "Finished!"
			if (y < (NG_STATSY - finished.mFont.GetHeight()*3/4) * CleanYfac) {
				// don't draw 'finished' if the level name is too tall
				y = drawPatchText(y, finished, "$FINISHED");
			}
		}

		return y;
	}

	int hcpDrawEL() {
		int y = TITLEY * CleanYfac;

		y = drawPatchText(y, entering, "$ENTERING");
		y += entering.mFont.GetHeight() * CleanYfac / 4;

		string levelName = lnametexts[1];
		Array<string> splitStrings;
		levelName.split(splitStrings, " - by: ");
		if (splitStrings.size() == 2) {
			y = drawNameStringOnly(y, splitStrings[0]);
			y -= 24;

			GIFont authorFont;
			authorFont.fontName = "smallfont";
			authorFont.color = "gold";
			patchInfo authorPatchInfo;
			authorPatchInfo.init(authorFont);
			y = drawPatchText(y, authorPatchInfo, splitStrings[1]);
		}
		return y;
	}
}

class TwentyHCPStatusScreenSP : TwentyHCPStatusScreenBase {
	int intermissioncounter;

	override void initStats ()
	{
		intermissioncounter = gameinfo.intermissioncounter;
		CurState = StatCount;
		acceleratestage = 0;
		sp_state = 1;
		cnt_kills[0] = cnt_items[0] = cnt_secret[0] = -1;
		cnt_time = cnt_par = -1;
		cnt_pause = Thinker.TICRATE;

		cnt_total_time = -1;
	}

	override void updateStats ()
	{
		if (acceleratestage && sp_state != 10)
		{
			acceleratestage = 0;
			sp_state = 10;
			PlaySound("intermission/nextstage");

			cnt_kills[0] = Plrs[me].skills;
			cnt_items[0] = Plrs[me].sitems;
			cnt_secret[0] = Plrs[me].ssecret;
			cnt_time = Thinker.Tics2Seconds(Plrs[me].stime);
			cnt_par = wbs.partime / Thinker.TICRATE;
			cnt_total_time = Thinker.Tics2Seconds(wbs.totaltime);
		}

		if (sp_state == 2)
		{
			if (intermissioncounter)
			{
				cnt_kills[0] += 2;

				if (!(bcnt&3))
					PlaySound("intermission/tick");
			}
			if (!intermissioncounter || cnt_kills[0] >= Plrs[me].skills)
			{
				cnt_kills[0] = Plrs[me].skills;
				PlaySound("intermission/nextstage");
				sp_state++;
			}
		}
		else if (sp_state == 4)
		{
			if (intermissioncounter)
			{
				cnt_items[0] += 2;

				if (!(bcnt&3))
					PlaySound("intermission/tick");
			}
			if (!intermissioncounter || cnt_items[0] >= Plrs[me].sitems)
			{
				cnt_items[0] = Plrs[me].sitems;
				PlaySound("intermission/nextstage");
				sp_state++;
			}
		}
		else if (sp_state == 6)
		{
			if (intermissioncounter)
			{
				cnt_secret[0] += 2;

				if (!(bcnt&3))
					PlaySound("intermission/tick");
			}
			if (!intermissioncounter || cnt_secret[0] >= Plrs[me].ssecret)
			{
				cnt_secret[0] = Plrs[me].ssecret;
				PlaySound("intermission/nextstage");
				sp_state++;
			}
		}
		else if (sp_state == 8)
		{
			if (intermissioncounter)
			{
				if (!(bcnt&3))
					PlaySound("intermission/tick");

				cnt_time += 3;
				cnt_par += 3;
				cnt_total_time += 3;
			}

			int sec = Thinker.Tics2Seconds(Plrs[me].stime);
			if (!intermissioncounter || cnt_time >= sec)
				cnt_time = sec;

			int tsec = Thinker.Tics2Seconds(wbs.totaltime);
			if (!intermissioncounter || cnt_total_time >= tsec)
				cnt_total_time = tsec;

			int psec = wbs.partime / Thinker.TICRATE;
			if (!intermissioncounter || cnt_par >= psec)
			{
				cnt_par = psec;

				if (cnt_time >= sec)
				{
					cnt_total_time = tsec;
					PlaySound("intermission/nextstage");
					sp_state++;
				}
			}
		}
		else if (sp_state == 10)
		{
			if (acceleratestage)
			{
				PlaySound("intermission/paststats");
				initShowNextLoc();
			}
		}
		else if (sp_state & 1)
		{
			if (!--cnt_pause){
				sp_state++;
				cnt_pause = Thinker.TICRATE;
			}
		}
	}

	override void drawStats() {
		// line height
		int lh = IntermissionFont.GetHeight() * 3 / 2;

		hcpDrawLF();

		screen.DrawText (BigFont, Font.CR_UNTRANSLATED, 50, 65, Stringtable.Localize("$TXT_IMKILLS"), DTA_Clean, true, DTA_Shadow, true);
		screen.DrawText (BigFont, Font.CR_UNTRANSLATED, 50, 90, Stringtable.Localize("$TXT_IMITEMS"), DTA_Clean, true, DTA_Shadow, true);
		screen.DrawText (BigFont, Font.CR_UNTRANSLATED, 50, 115, Stringtable.Localize("$TXT_IMSECRETS"), DTA_Clean, true, DTA_Shadow, true);

		int countpos = gameinfo.gametype == GAME_Strife ? 285 : 270;
		if (sp_state >= 2) {
			drawPercent (IntermissionFont, countpos, 65, cnt_kills[0], wbs.maxkills);
		}
		if (sp_state >= 4) {
			drawPercent (IntermissionFont, countpos, 90, cnt_items[0], wbs.maxitems);
		}
		if (sp_state >= 6) {
			drawPercent (IntermissionFont, countpos, 115, cnt_secret[0], wbs.maxsecret);
		}
		if (sp_state >= 8) {
			screen.DrawText (BigFont, Font.CR_UNTRANSLATED, 85, 160, Stringtable.Localize("$TXT_IMTIME"), DTA_Clean, true, DTA_Shadow, true);
			drawTime (249, 160, cnt_time);
			if (wi_showtotaltime) {
				drawTime (249, 180, cnt_total_time);
			}
		}
	}
}

class TwentyHCPStatusScreenCoop : TwentyHCPStatusScreenBase {
	int textcolor;

	//====================================================================
	//
	//
	//
	//====================================================================

	override void initStats ()
	{
		textcolor = (gameinfo.gametype & GAME_Raven) ? Font.CR_GREEN : Font.CR_UNTRANSLATED;

		CurState = StatCount;
		acceleratestage = 0;
		ng_state = 1;

		cnt_pause = Thinker.TICRATE;

		for (int i = 0; i < MAXPLAYERS; i++)
		{
			playerready[i] = false;
			cnt_kills[i] = cnt_items[i] = cnt_secret[i] = cnt_frags[i] = 0;

			if (!playeringame[i])
				continue;

			dofrags += fragSum (i);
		}

		dofrags = !!dofrags;
	}

	//====================================================================
	//
	//
	//
	//====================================================================

	override void updateStats ()
	{

		int i;
		int fsum;
		bool stillticking;
		bool autoskip = autoSkip();

		if ((acceleratestage || autoskip) && ng_state != 10)
		{
			acceleratestage = 0;

			for (i=0 ; i<MAXPLAYERS ; i++)
			{
				if (!playeringame[i])
					continue;

				cnt_kills[i] = Plrs[i].skills;
				cnt_items[i] = Plrs[i].sitems;
				cnt_secret[i] = Plrs[i].ssecret;

				if (dofrags)
					cnt_frags[i] = fragSum (i);
			}
			PlaySound("intermission/nextstage");
			ng_state = 10;
		}

		if (ng_state == 2)
		{
			if (!(bcnt&3))
				PlaySound("intermission/tick");

			stillticking = false;

			for (i=0 ; i<MAXPLAYERS ; i++)
			{
				if (!playeringame[i])
					continue;

				cnt_kills[i] += 2;

				if (cnt_kills[i] > Plrs[i].skills)
					cnt_kills[i] = Plrs[i].skills;
				else
					stillticking = true;
			}

			if (!stillticking)
			{
				PlaySound("intermission/nextstage");
				ng_state++;
			}
		}
		else if (ng_state == 4)
		{
			if (!(bcnt&3))
				PlaySound("intermission/tick");

			stillticking = false;

			for (i=0 ; i<MAXPLAYERS ; i++)
			{
				if (!playeringame[i])
					continue;

				cnt_items[i] += 2;
				if (cnt_items[i] > Plrs[i].sitems)
					cnt_items[i] = Plrs[i].sitems;
				else
					stillticking = true;
			}
			if (!stillticking)
			{
				PlaySound("intermission/nextstage");
				ng_state++;
			}
		}
		else if (ng_state == 6)
		{
			if (!(bcnt&3))
				PlaySound("intermission/tick");

			stillticking = false;

			for (i=0 ; i<MAXPLAYERS ; i++)
			{
				if (!playeringame[i])
					continue;

				cnt_secret[i] += 2;

				if (cnt_secret[i] > Plrs[i].ssecret)
					cnt_secret[i] = Plrs[i].ssecret;
				else
					stillticking = true;
			}

			if (!stillticking)
			{
				PlaySound("intermission/nextstage");
				ng_state += 1 + 2*!dofrags;
			}
		}
		else if (ng_state == 8)
		{
			if (!(bcnt&3))
				PlaySound("intermission/tick");

			stillticking = false;

			for (i=0 ; i<MAXPLAYERS ; i++)
			{
				if (!playeringame[i])
					continue;

				cnt_frags[i] += 1;

				if (cnt_frags[i] >= (fsum = fragSum(i)))
					cnt_frags[i] = fsum;
				else
					stillticking = true;
			}

			if (!stillticking)
			{
				PlaySound("intermission/cooptotal");
				ng_state++;
			}
		}
		else if (ng_state == 10)
		{
			int i;
			for (i = 0; i < MAXPLAYERS; i++)
			{
				// If the player is in the game and not ready, stop checking
				if (playeringame[i] && players[i].Bot == NULL && !playerready[i])
					break;
			}

			// All players are ready; proceed.
			if ((i == MAXPLAYERS && acceleratestage) || autoskip)
			{
				PlaySound("intermission/pastcoopstats");
				initShowNextLoc();
			}
		}
		else if (ng_state & 1)
		{
			if (!--cnt_pause)
			{
				ng_state++;
				cnt_pause = Thinker.TICRATE;
			}
		}
	}

	//====================================================================
	//
	//
	//
	//====================================================================

	override void drawStats ()
	{
		int i, x, y, ypadding, height, lineheight;
		int maxnamewidth, maxscorewidth, maxiconheight;
		int pwidth = IntermissionFont.GetCharWidth("%");
		int icon_x, name_x, kills_x, bonus_x, secret_x;
		int bonus_len, secret_len;
		int missed_kills, missed_items, missed_secrets;
		float h, s, v, r, g, b;
		int color;
		String text_bonus, text_secret, text_kills;
		TextureID readyico = TexMan.CheckForTexture("READYICO", TexMan.Type_MiscPatch);

		y = hcpDrawLF();

		[maxnamewidth, maxscorewidth, maxiconheight] = GetPlayerWidths();
		// Use the readyico height if it's bigger.
		Vector2 readysize = TexMan.GetScaledSize(readyico);
		Vector2 readyoffset = TexMan.GetScaledOffset(readyico);
		height = int(readysize.Y - readyoffset.Y);
		maxiconheight = MAX(height, maxiconheight);
		height = SmallFont.GetHeight() * CleanYfac;
		lineheight = MAX(height, maxiconheight * CleanYfac);
		ypadding = (lineheight - height + 1) / 2;
		y += CleanYfac;

		text_bonus = Stringtable.Localize((gameinfo.gametype & GAME_Raven) ? "$SCORE_BONUS" : "$SCORE_ITEMS");
		text_secret = Stringtable.Localize("$SCORE_SECRET");
		text_kills = Stringtable.Localize("$SCORE_KILLS");

		icon_x = 8 * CleanXfac;
		name_x = icon_x + maxscorewidth * CleanXfac;
		kills_x = name_x + (maxnamewidth + MAX(SmallFont.StringWidth("XXXXX"), SmallFont.StringWidth(text_kills)) + 8) * CleanXfac;
		bonus_x = kills_x + ((bonus_len = SmallFont.StringWidth(text_bonus)) + 8) * CleanXfac;
		secret_x = bonus_x + ((secret_len = SmallFont.StringWidth(text_secret)) + 8) * CleanXfac;

		x = (screen.GetWidth() - secret_x) >> 1;
		icon_x += x;
		name_x += x;
		kills_x += x;
		bonus_x += x;
		secret_x += x;


		screen.DrawText(SmallFont, textcolor, name_x, y, Stringtable.Localize("$SCORE_NAME"), DTA_CleanNoMove, true);
		screen.DrawText(SmallFont, textcolor, kills_x - SmallFont.StringWidth(text_kills)*CleanXfac, y, text_kills, DTA_CleanNoMove, true);
		screen.DrawText(SmallFont, textcolor, bonus_x - bonus_len*CleanXfac, y, text_bonus, DTA_CleanNoMove, true);
		screen.DrawText(SmallFont, textcolor, secret_x - secret_len*CleanXfac, y, text_secret, DTA_CleanNoMove, true);
		y += height + 6 * CleanYfac;

		missed_kills = wbs.maxkills;
		missed_items = wbs.maxitems;
		missed_secrets = wbs.maxsecret;

		// Draw lines for each player
		for (i = 0; i < MAXPLAYERS; ++i)
		{
			if (!playeringame[i])
				continue;

			PlayerInfo player = players[i];

			screen.Dim(player.GetDisplayColor(), 0.8f, x, y - ypadding, (secret_x - x) + (8 * CleanXfac), lineheight);

			if (playerready[i] || player.Bot != NULL) // Bots are automatically assumed ready, to prevent confusion
				screen.DrawTexture(readyico, true, x - (readysize.Y * CleanXfac), y, DTA_CleanNoMove, true);

			Color thiscolor = GetRowColor(player, i == consoleplayer);
			if (player.mo.ScoreIcon.isValid())
			{
				screen.DrawTexture(player.mo.ScoreIcon, true, icon_x, y, DTA_CleanNoMove, true);
			}
			screen.DrawText(SmallFont, thiscolor, name_x, y + ypadding, player.GetUserName(), DTA_CleanNoMove, true);
			drawPercent(SmallFont, kills_x, y + ypadding, cnt_kills[i], wbs.maxkills, false, thiscolor);
			missed_kills -= cnt_kills[i];
			if (ng_state >= 4)
			{
				drawPercent(SmallFont, bonus_x, y + ypadding, cnt_items[i], wbs.maxitems, false, thiscolor);
				missed_items -= cnt_items[i];
				if (ng_state >= 6)
				{
					drawPercent(SmallFont, secret_x, y + ypadding, cnt_secret[i], wbs.maxsecret, false, thiscolor);
					missed_secrets -= cnt_secret[i];
				}
			}
			y += lineheight + CleanYfac;
		}

		// Draw "MISSED" line
		y += 3 * CleanYfac;
		screen.DrawText(SmallFont, Font.CR_DARKGRAY, name_x, y, Stringtable.Localize("$SCORE_MISSED"), DTA_CleanNoMove, true);
		drawPercent(SmallFont, kills_x, y, missed_kills, wbs.maxkills, false, Font.CR_DARKGRAY);
		if (ng_state >= 4)
		{
			drawPercent(SmallFont, bonus_x, y, missed_items, wbs.maxitems, false, Font.CR_DARKGRAY);
			if (ng_state >= 6)
			{
				drawPercent(SmallFont, secret_x, y, missed_secrets, wbs.maxsecret, false, Font.CR_DARKGRAY);
			}
		}

		// Draw "TOTAL" line
		y += height + 3 * CleanYfac;
		screen.DrawText(SmallFont, textcolor, name_x, y, Stringtable.Localize("$SCORE_TOTAL"), DTA_CleanNoMove, true);
		drawNum(SmallFont, kills_x, y, wbs.maxkills, 0, false, textcolor);
		if (ng_state >= 4)
		{
			drawNum(SmallFont, bonus_x, y, wbs.maxitems, 0, false, textcolor);
			if (ng_state >= 6)
			{
				drawNum(SmallFont, secret_x, y, wbs.maxsecret, 0, false, textcolor);
			}
		}
	}
}

class TwentyHCPStatusScreenDM : TwentyHCPStatusScreenBase {
	int textcolor;

	//====================================================================
	//
	//
	//
	//====================================================================

	override void initStats (void)
	{
		int i, j;

		textcolor = (gameinfo.gametype & GAME_Raven) ? Font.CR_GREEN : Font.CR_UNTRANSLATED;

		CurState = StatCount;
		acceleratestage = 0;
		for(i = 0; i < MAXPLAYERS; i++)
		{
			playerready[i] = false;
			cnt_frags[i] = cnt_deaths[i] = player_deaths[i] = 0;
		}
		total_frags = 0;
		total_deaths = 0;

		ng_state = 1;
		cnt_pause = Thinker.TICRATE;

		for (i=0 ; i<MAXPLAYERS ; i++)
		{
			if (playeringame[i])
			{
				for (j = 0; j < MAXPLAYERS; j++)
				{
					if (playeringame[j])
						player_deaths[i] += Plrs[j].frags[i];
				}
				total_deaths += player_deaths[i];
				total_frags += Plrs[i].fragcount;
			}
		}
	}

	override void updateStats ()
	{

		int i;
		bool stillticking;
		bool doautoskip = autoSkip();

		if ((acceleratestage || doautoskip) && ng_state != 6)
		{
			acceleratestage = 0;

			for (i = 0; i<MAXPLAYERS; i++)
			{
				if (!playeringame[i]) continue;

				cnt_frags[i] = Plrs[i].fragcount;
				cnt_deaths[i] = player_deaths[i];
			}
			PlaySound("intermission/nextstage");
			ng_state = 6;
		}

		if (ng_state == 2)
		{
			if (!(bcnt & 3))
				PlaySound("intermission/tick");

			stillticking = false;

			for (i = 0; i<MAXPLAYERS; i++)
			{
				if (!playeringame[i])
					continue;

				cnt_frags[i] += 2;

				if (cnt_frags[i] > Plrs[i].fragcount)
					cnt_frags[i] = Plrs[i].fragcount;
				else
					stillticking = true;
			}

			if (!stillticking)
			{
				PlaySound("intermission/nextstage");
				ng_state++;
			}
		}
		else if (ng_state == 4)
		{
			if (!(bcnt & 3))
				PlaySound("intermission/tick");

			stillticking = false;

			for (i = 0; i<MAXPLAYERS; i++)
			{
				if (!playeringame[i])
					continue;

				cnt_deaths[i] += 2;
				if (cnt_deaths[i] > player_deaths[i])
					cnt_deaths[i] = player_deaths[i];
				else
					stillticking = true;
			}
			if (!stillticking)
			{
				PlaySound("intermission/nextstage");
				ng_state++;
			}
		}
		else if (ng_state == 6)
		{
			int i;
			for (i = 0; i < MAXPLAYERS; i++)
			{
				// If the player is in the game and not ready, stop checking
				if (playeringame[i] && players[i].Bot == NULL && !playerready[i])
					break;
			}

			// All players are ready; proceed.
			if ((i == MAXPLAYERS && acceleratestage) || doautoskip)
			{
				PlaySound("intermission/pastdmstats");
				initShowNextLoc();
			}
		}
		else if (ng_state & 1)
		{
			if (!--cnt_pause)
			{
				ng_state++;
				cnt_pause = Thinker.TICRATE;
			}
		}
	}

	override void drawStats ()
	{
		int i, pnum, x, y, ypadding, height, lineheight;
		int maxnamewidth, maxscorewidth, maxiconheight;
		int pwidth = IntermissionFont.GetCharWidth("%");
		int icon_x, name_x, frags_x, deaths_x;
		int deaths_len;
		String text_deaths, text_frags;
		TextureID readyico = TexMan.CheckForTexture("READYICO", TexMan.Type_MiscPatch);

		y = hcpDrawLF();

		[maxnamewidth, maxscorewidth, maxiconheight] = GetPlayerWidths();
		// Use the readyico height if it's bigger.
		Vector2 readysize = TexMan.GetScaledSize(readyico);
		Vector2 readyoffset = TexMan.GetScaledOffset(readyico);
		height = int(readysize.Y - readyoffset.Y);
		maxiconheight = MAX(height, maxiconheight);
		height = SmallFont.GetHeight() * CleanYfac;
		lineheight = MAX(height, maxiconheight * CleanYfac);
		ypadding = (lineheight - height + 1) / 2;
		y += CleanYfac;

		text_deaths = Stringtable.Localize("$SCORE_DEATHS");
		//text_color = Stringtable.Localize("$SCORE_COLOR");
		text_frags = Stringtable.Localize("$SCORE_FRAGS");

		icon_x = 8 * CleanXfac;
		name_x = icon_x + maxscorewidth * CleanXfac;
		frags_x = name_x + (maxnamewidth + MAX(SmallFont.StringWidth("XXXXX"), SmallFont.StringWidth(text_frags)) + 8) * CleanXfac;
		deaths_x = frags_x + ((deaths_len = SmallFont.StringWidth(text_deaths)) + 8) * CleanXfac;

		x = (Screen.GetWidth() - deaths_x) >> 1;
		icon_x += x;
		name_x += x;
		frags_x += x;
		deaths_x += x;

		screen.DrawText(SmallFont, textcolor, name_x, y, Stringtable.Localize("$SCORE_NAME"), DTA_CleanNoMove, true);
		screen.DrawText(SmallFont, textcolor, frags_x - SmallFont.StringWidth(text_frags) * CleanXfac, y, text_frags, DTA_CleanNoMove, true);
		screen.DrawText(SmallFont, textcolor, deaths_x - deaths_len * CleanXfac, y, text_deaths, DTA_CleanNoMove, true);
		y += height + 6 * CleanYfac;

		// Sort all players
		Array<int> sortedplayers;
		GetSortedPlayers(sortedplayers, teamplay);

		// Draw lines for each player
		for (i = 0; i < sortedplayers.Size(); i++)
		{
			pnum = sortedplayers[i];
			PlayerInfo player = players[pnum];

			if (!playeringame[pnum])
				continue;

			screen.Dim(player.GetDisplayColor(), 0.8, x, y - ypadding, (deaths_x - x) + (8 * CleanXfac), lineheight);

			if (playerready[pnum] || player.Bot != NULL) // Bots are automatically assumed ready, to prevent confusion
				screen.DrawTexture(readyico, true, x - (readysize.X * CleanXfac), y, DTA_CleanNoMove, true);

			let thiscolor = GetRowColor(player, pnum == consoleplayer);
			if (player.mo.ScoreIcon.isValid())
			{
				screen.DrawTexture(player.mo.ScoreIcon, true, icon_x, y, DTA_CleanNoMove, true);
			}
			screen.DrawText(SmallFont, thiscolor, name_x, y + ypadding, player.GetUserName(), DTA_CleanNoMove, true);
			drawNum(SmallFont, frags_x, y + ypadding, cnt_frags[pnum], 0, false, thiscolor);
			if (ng_state >= 2)
			{
				drawNum(SmallFont, deaths_x, y + ypadding, cnt_deaths[pnum], 0, false, thiscolor);
			}
			y += lineheight + CleanYfac;
		}

		// Draw "TOTAL" line
		y += height + 3 * CleanYfac;
		screen.DrawText(SmallFont, textcolor, name_x, y, Stringtable.Localize("$SCORE_TOTAL"), DTA_CleanNoMove, true);
		drawNum(SmallFont, frags_x, y, total_frags, 0, false, textcolor);
		if (ng_state >= 4)
		{
			drawNum(SmallFont, deaths_x, y, total_deaths, 0, false, textcolor);
		}

		// Draw game time
		y += height + CleanYfac;

		int seconds = Thinker.Tics2Seconds(Plrs[me].stime);
		int hours = seconds / 3600;
		int minutes = (seconds % 3600) / 60;
		seconds = seconds % 60;

		String leveltime = Stringtable.Localize("$SCORE_LVLTIME") .. ": " .. String.Format("%02i:%02i:%02i", hours, minutes, seconds);

		screen.DrawText(SmallFont, textcolor, x, y, leveltime, DTA_CleanNoMove, true);
	}
}