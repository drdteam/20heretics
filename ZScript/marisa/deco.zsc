/* Decorations for my map - MK */

/* Redefined light emitters, for giving them custom lights */
Class mkKeyGizmoYellow : Actor
{
	Default
	{
		//$Title "Yellow Key Marker"
		//$Category "Marisa Kirisame"
		Radius 16;
		Height 50;
		+SOLID
	}
	States
	{
	Spawn:
		KGZ1 A 1;
		KGZ1 A 1 A_SpawnItemEx("KeyGizmoFloatYellow",0,0,60);
		KGZ1 A -1;
		Stop;
	}
}
Class mkKeyGizmoGreen : Actor
{
	Default
	{
		//$Title "Green Key Marker"
		//$Category "Marisa Kirisame"
		Radius 16;
		Height 50;
		+SOLID
	}
	States
	{
	Spawn:
		KGZ1 A 1;
		KGZ1 A 1 A_SpawnItemEx("KeyGizmoFloatGreen",0,0,60);
		KGZ1 A -1;
		Stop;
	}
}
Class mkKeyGizmoBlue : Actor
{
	Default
	{
		//$Title "Blue Key Marker"
		//$Category "Marisa Kirisame"
		Radius 16;
		Height 50;
		+SOLID
	}
	States
	{
	Spawn:
		KGZ1 A 1;
		KGZ1 A 1 A_SpawnItemEx("KeyGizmoFloatBlue",0,0,60);
		KGZ1 A -1;
		Stop;
	}
}

/* toggleable lights */
Class mkFireBrazier : SwitchableDecoration
{
	Default
	{
		//$Title "Toggleable Fire Brazier"
		//$Category "Marisa Kirisame"
		Radius 16;
		Height 44;
		+SOLID
	}
	States
	{
	Spawn:
		KFR1 I 0;
		Goto Inactive;
	Active:
		KFR1 A 0
		{
			if ( !args[0] )
			{
				A_PlaySound("hexen/ignite",0,0.8);
			}
			args[0] = 0;
			A_PlaySound("thief/firelp",CHAN_BODY,1.0,true,4.0);
		}
		KFR1 ABCDEFGH 3;
		Goto Active+1;
	Inactive:
		KFR1 I 0
		{
			if ( args[0] )
			{
				return ResolveState("Active");
			}
			A_StopSound(CHAN_BODY);
			return ResolveState(null);
		}
		KFR1 I -1;
		Stop;
	}
}
Class mkSerpentTorch : SwitchableDecoration
{
	Default
	{
		//$Title "Toggleable Serpent Torch"
		//$Category "Marisa Kirisame"
		Radius 12;
		Height 54;
		+SOLID
	}
	States
	{
	Spawn:
		SRTC D 0;
		Goto Inactive;
	Active:
		SRTC A 0
		{
			if ( !args[0] )
			{
				A_PlaySound("hexen/ignite",0,0.8);
			}
			args[0] = 0;
		}
		SRTC ABC 4;
		Goto Active+1;
	Inactive:
		SRTC D 0
		{
			if ( args[0] )
			{
				return ResolveState("Active");
			}
			return ResolveState(null);
		}
		SRTC D -1;
		Stop;
	}
}
Class mkWallTorch : SwitchableDecoration
{
	Default
	{
		//$Title "Toggleable Wall Torch"
		//$Category "Marisa Kirisame"
		Radius 6;
		Height 16;
		+NOGRAVITY
	}
	States
	{
	Spawn:
		WTRH D 0;
		Goto Inactive;
	Active:
		WTRH A 0
		{
			if ( !args[0] )
			{
				A_PlaySound("hexen/ignite",0,0.8);
			}
			args[0] = 0;
		}
		WTRH ABC 6;
		Goto Active+1;
	Inactive:
		WTRH D 0
		{
			if ( args[0] )
			{
				return ResolveState("Active");
			}
			return ResolveState(null);
		}
		WTRH D -1;
		Stop;
	}
}
Class mkChandelier : SwitchableDecoration
{
	Default
	{
		//$Title "Toggleable Chandelier"
		//$Category "Marisa Kirisame"
		Radius 20;
		Height 60;
		+SPAWNCEILING
		+NOGRAVITY
	}
	States
	{
	Spawn:
		CHDL D 0;
		Goto Inactive;
	Active:
		CHDL A 0
		{
			if ( !args[0] )
			{
				A_PlaySound("hexen/ignite",0,0.1);
			}
			args[0] = 0;
		}
		CHDL ABC 4;
		Goto Active+1;
	Inactive:
		CHDL D 0
		{
			if ( args[0] )
			{
				return ResolveState("Active");
			}
			return ResolveState(null);
		}
		CHDL D -1;
		Stop;
	}
}
Class mkHeartPedestal : Actor
{
	override bool Used( Actor user )
	{
		if ( !user || !(user is "PlayerPawn") ) return false;
		if ( CurState == ResolveState("Active") ) return false;
		level.ExecuteSpecial(special,user,null,0,args[0],args[1],args[2],args[3],args[4]);
		SetStateLabel("Active");
		return true;
	}

	Default
	{
		//$Title "Heart of D'Sparil (Pedestal)"
		//$Category "Marisa Kirisame"
		Radius 10;
		Height 40;
		+SOLID
	}
	States
	{
	Spawn:
		GMPD B 0;
		GMPD B 1 A_PlaySound("thief/egglp1",CHAN_BODY,1.0,true,3.0);
		GMPD B -1;
		Stop;
	Active:
		GMPD A 0;
		GMPD A 1 A_StopSound(CHAN_BODY);
		GMPD A -1;
		Stop;
	}
}
Class mkHeart : Actor
{
	Actor myactivator;
	bool activated;
	int i;

	override void Activate( Actor activator )
	{
		if ( activated ) return;
		activated = true;
		myactivator = activator;
		SetStateLabel("Active");
	}
	Default
	{
		//$Title "Heart of D'Sparil"
		//$Category "Marisa Kirisame"
		Radius 12;
		Height 10;
	}
	States
	{
	Spawn:
		ABGM A 0;
		ABGM A 1 A_PlaySound("thief/egglp1",CHAN_BODY,1.0,true,3.0);
		ABGM A -1;
		Stop;
	Active:
		ABGM A 0
		{
			bNoGravity = true;
			vel.z = 0.25;
			A_PlaySound("heart/float",CHAN_6,1.0,true);
		}
		ABGM A 3 Bright
		{
			for ( int i=0; i<16; i++ ) A_SpawnParticle("Red",SPF_FULLBRIGHT|SPF_RELPOS,30,FRandom(1,5),FRandom(0,360),FRandom(0,8),0,FRandom(0,12),accelz:0.1);
			if ( i++ > 100 ) return ResolveState("GTFO");
			if ( !(i%30) )
			{
				for ( int i=0; i<360; i++ ) A_SpawnParticle("Red",SPF_FULLBRIGHT,40,5,zoff:7,velx:cos(i)*2.0,vely:sin(i)*2.0,sizestep:0.2);
				A_PlaySound("heart/ring",CHAN_7,0.3);
			}
			return ResolveState(null);
		}
		Wait;
	GTFO:
		TNT1 A 0
		{
			A_StopSound(CHAN_BODY);
			A_PlaySound("heart/burst",CHAN_6,1.0,false,ATTN_NONE);
		}
		TNT1 AAAAA 1
		{
			for ( int i=0; i<64; i++ ) A_SpawnParticle("Red",SPF_FULLBRIGHT,100,FRandom(4,16),zoff:7,velx:FRandom(-4,4),vely:FRandom(-4,4),velz:FRandom(-4,4),accelz:0.05);
			for ( int i=0; i<360; i++ ) A_SpawnParticle("Red",SPF_FULLBRIGHT,80,5,zoff:7,velx:cos(i)*4.0,vely:sin(i)*4.0,sizestep:0.2);
			A_PlaySound("heart/ring",CHAN_7,0.6);
			level.ExecuteSpecial(special,myactivator,null,0,args[0],args[1],args[2],args[3],args[4]);
		}
		TNT1 B 4;
		TNT1 C 2;
		Stop;
	}
}
Class mkDSparilCorpse : Actor
{
	Default
	{
		//$Title "Corpse of D'Sparil"
		//$Category "Marisa Kirisame"
		Radius 32;
		Height 8;
	}
	States
	{
	Spawn:
		SDTH O -1;
		Stop;
	}
}
