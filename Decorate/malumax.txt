actor Malumax 1010
{
	//$Category "Monsters"
	Health 3500
	Radius 24
	Height 105
	Scale 1.5
	Mass 300
	Speed 20
	PainChance 64
	Monster
	+DROPOFF
	+BOSS
	+DONTMORPH
	+FULLVOLACTIVE
	+NORADIUSDMG
	+NOTARGET
	+NOICEDEATH
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "wizard/sight"
	AttackSound "wizard/attack"
	PainSound "wizard/pain"
	ActiveSound "wizard/active"
	DeathSound "wizard/death"

	Obituary "%o had an encounter with Malumax."

	var int user_cataclysm_state;
	var int user_x;

	States
	{
	Spawn:
		MALM A 0 NoDelay { user_cataclysm_state = 0; }
		MALM AB 12 A_Look
		loop
	See:
		MALM AABBAACC 4 A_Chase
		MALM A 0 A_Jump(8, "Teleport")
		loop
	Pain:
		MALM D 0 A_Jump(64, "Teleport")
		MALM D 3
		MALM D 3 A_Pain
		goto See
	Missile:
		MALM E 0 {
			if (user_cataclysm_state == 0 && ((1.0 * health / GetSpawnHealth()) < .8))
			{
				ACS_Execute(996, 0, 1);
			}
		}
		MALM E 0 A_Jump(32, "Teleport")
		MALM E 0 A_Jump(256, "Hydra", "HomingAttack")
		goto See
	Teleport:
		MALM AAAFFFAAAGGGAAAFFFAAAGGG 2 {
			A_FaceTarget;
			for (user_x = random(-5, 22); user_x < 360; user_x+=random(22, 65))
			{
				A_SpawnProjectile("PhoenixFX2", 5, 0, user_x, CMF_ABSOLUTEPITCH);
			}
		}
		MALM E 0 {
			A_SpawnItem("TeleportFog", 0, 32);
			A_ChangeFlag("INVISIBLE", 1);
			A_ChangeFlag("SHOOTABLE", 0);
		}
		MALM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_Wander
		MALM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_Wander
		MALM E 0 {
			A_SpawnItem("TeleportFog", 0, 32);
			A_ChangeFlag("INVISIBLE", 0);
			A_ChangeFlag("SHOOTABLE", 1);
		}
		MALM AAAFFFAAAGGGAAAFFFAAAGGG 2 {
			A_RadiusThrust(50, 128, RTF_NOTMISSILE | RTF_THRUSTZ, 1);
			A_FaceTarget;
			for (user_x = random(-5, 22); user_x < 360; user_x+=random(22, 65))
			{
				A_SpawnProjectile("PhoenixFX2", 5, 0, user_x, CMF_ABSOLUTEPITCH);
			}
		}
		goto See		
	Hydra:
		MALM E 0 A_CheckRange(512, "HomingAttack")
		MALM E 4 A_FaceTarget
		MALM E 12 A_SpawnProjectile("BawssHydra", 32, 0, random(0, 360), CMF_ABSOLUTEPITCH, 60)
		goto See
	HomingAttack:
		MALM E 4 A_FaceTarget
		MALM E 12 A_SpawnProjectile("BawssHoming")
		goto See
	Death:
		FDTH C 6 { A_Scream; A_NoBlocking; }
		FDTH DEFGHIJKLMNOPQR 6
		TNT1 A 1 ACS_Execute(996, 0, 0)
		Stop
	}
}

actor BawssHoming
{
	Radius 10
	Height 6
	Speed 10
	FastSpeed 15
	Damage 9
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	+SEEKERMISSILE
	RenderStyle Normal

	States
	{
	Spawn:
		SBS1 ABCD 3 Bright A_SeekerMissile(5, 10, SMF_PRECISE)
		Loop
	Death:
		FX06 DEFG 5 Bright
		Stop
	}
}

actor BawssHydra
{
	Radius 10
	Height 6
	Speed 10
	FastSpeed 15
	Damage 9
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	RenderStyle Normal

	States
	{
	Spawn:
		SBS1 ABCD 3 Bright
		Loop
	Death:
		FX06 DEFG 5 Bright
		FX06 G 0 A_SpawnItem("Hydra1")
		Stop
	}
}

actor Hydra1
{
	Health 10
	Speed 0
	Monster
	+NODAMAGE
	-SOLID
	-SHOOTABLE
	RenderStyle Add
	Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.50,0.25]"
	+ALWAYSFAST

	States
	{
	Spawn:
		SSDV JJJJJJJJJJ 1 A_Look
		SSDV J 1 A_Die
		Goto Death
	See:
		SSDV JIHEA 5
	See2:
		SSDV AAAAAAAAAAAAAAAAA 5 A_Chase
		SSDV A 1 A_Die
		Goto Death
	Missile:
		SSDV ABC 10 A_FaceTarget
		SSDV DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD 2 A_SpawnProjectile("PhoenixFX2", 24)
		SSDV D 0 A_Die
		goto Death
	Death:
		SSDV AEHIJ 5
		Stop
	}
}
